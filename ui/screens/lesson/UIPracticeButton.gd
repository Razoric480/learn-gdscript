class_name UIPracticeButton
extends Node

var completed_before := false: set = set_completed_before
var is_highlighted := false: set = set_is_highlighted
var navigation_disabled := false: set = set_navigation_disabled

var _practice: Practice

@export var _title_label: Label
@onready var _next_pill_label := $Margin/Row/Column/Row/NextPill as Label
@export var _description_label: RichTextLabel
@onready var _completed_before_icon := $Margin/Row/Column/Row/CompletedBeforeIcon as TextureRect
@onready var _navigate_button := $Margin/Row/NavigateButton as Button
@onready var _no_navigation_label := $Margin/Row/NoNavigationLabel as Label


func _ready() -> void:
	_completed_before_icon.visible = completed_before


func _notification(what: int) -> void:
	if what == NOTIFICATION_TRANSLATION_CHANGED:
		_update_labels()


func setup(practice: Practice, practice_index: int) -> void:
	_practice = practice

	if not is_inside_tree():
		await self.ready

	_title_label.text = "%d. %s" % [practice_index + 1, tr(_practice.title).capitalize()]
	_description_label.text = TextUtils.tr_paragraph(_practice.description)
	_description_label.visible = not _practice.description.is_empty()
	_navigate_button.connect("pressed", Callable(Events, "emit_signal").bind("practice_requested", _practice))


func _update_labels() -> void:
	if not _practice:
		return

	_title_label.text = tr(_practice.title).capitalize()
	_description_label.text = TextUtils.tr_paragraph(_practice.description)


func set_completed_before(value: bool) -> void:
	completed_before = value
	if not _completed_before_icon:
		await self.ready

	_completed_before_icon.visible = completed_before


func set_is_highlighted(value: bool) -> void:
	is_highlighted = value

	if not is_inside_tree():
		await self.ready

	_next_pill_label.visible = is_highlighted


func set_navigation_disabled(value: bool) -> void:
	navigation_disabled = value

	if not is_inside_tree():
		await self.ready

	_navigate_button.visible = not navigation_disabled
	_no_navigation_label.visible = navigation_disabled
